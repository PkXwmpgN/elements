<?xml version="1.0" encoding="UTF-8"?>
<program name="positions_process">
    <shaders>
        <vertex><![CDATA[
            attribute vec2 a_vertex_xy;
            varying vec2 v_uv;

            void main()
            {
                gl_Position = vec4(a_vertex_xy, 0.0, 1.0);
                v_uv = a_vertex_xy * 0.5 + 0.5;
            }
        ]]></vertex>

        <fragment><![CDATA[
            precision highp float;

            varying vec2 v_uv;

            uniform sampler2D u_positions;
            uniform sampler2D u_velocities;
            uniform float u_dt;

            vec4 pack(vec2 value)
            {
                vec2 shift = vec2(255.0, 1.0);
                vec2 mask = vec2(0.0, 0.00392156863);

                vec4 result = fract(value.xxyy * shift.xyxy);
                result -= result.xxzz * mask.xyxy;

                if(value.x == 1.0)
                    result.xy = vec2(0.0, 1.0);
                if(value.y == 1.0)
                    result.zw = vec2(0.0, 1.0);

                return result;
            }

            vec2 unpack(vec4 value)
            {
                vec2 shift = vec2(0.00392156863, 1.0);
                return vec2(dot(value.xy, shift), dot(value.zw, shift));
            }

            void main()
            {
                vec2 position = unpack(texture2D(u_positions, v_uv));
                vec2 velocity = unpack(texture2D(u_velocities, position));

                velocity = velocity * 2.0 - 1.0;
                position = position * 2.0 - 1.0;

                position += velocity * u_dt;
                position = clamp(position, vec2(-1.0), vec2(1.0));

                gl_FragColor = pack(position * 0.5 + 0.5);
            }

        ]]></fragment>
    </shaders>

    <a_locations>
        <location name="a_vertex_xy" index="0"/>
    </a_locations>

    <u_locations>
        <location name="u_positions" index="0"/>
        <location name="u_velocities" index="1"/>
        <location name="u_dt" index="2"/>
    </u_locations>

</program>